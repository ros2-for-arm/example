cmake_minimum_required(VERSION 3.5)
project(optee_ta_security)

find_package(ament_cmake REQUIRED)

# This is defined into the Makefile and can be redefined. It should match the
# TA_TRUSTED_UUID set in include/ta_public.h
set(TA_BINARY_NAME aed3b02c-8e42-11e7-bb31be2e44b06b34)

# Check if the mandatory folders are defined
message("Using OPTEE_CLIENT_EXPORT=" ${OPTEE_CLIENT_EXPORT})
message("Using OPTEE_OS_EXPORT=" ${OPTEE_OS_EXPORT})
if((NOT DEFINED OPTEE_CLIENT_EXPORT) OR (NOT DEFINED OPTEE_OS_EXPORT)
    OR (NOT IS_DIRECTORY ${OPTEE_CLIENT_EXPORT})
    OR (NOT IS_DIRECTORY ${OPTEE_OS_EXPORT}))
  message(WARNING "OPTEE_OS_EXPORT and OPTEE_CLIENT_EXPORT must be defined to compile ${PROJECT_NAME} -- Skipping this package")
  return()
endif()

set(TA_SRC_C src/ta_aes.c
            src/ta_mainpoint.c
            src/ta_rsa.c
            src/ta_sha.c
            src/ta_utils.c)

# Set the makefile command and the directory where to launch it
# The TA is generated by calling the .mk submake file: ta_dev_kit.mk
add_custom_command(
  OUTPUT ${TA_BINARY_NAME}
  PRE_BUILD
  COMMAND $(MAKE) clean
  COMMAND $(MAKE) BINARY=${TA_BINARY_NAME}
               CROSS_COMPILE=$ENV{CROSS_COMPILE}
               TEEC_EXPORT=${OPTEE_CLIENT_EXPORT}
               TA_DEV_KIT_DIR=${OPTEE_OS_EXPORT}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src
  COMMENT "Generating trusted application"
  DEPENDS ${TA_SRC_C}
)

# Compile the TA inside the package
add_custom_target(do_ta ALL DEPENDS ${TA_BINARY_NAME})

ament_export_include_directories(include)

# Copy the include/ folder content into install/include/${PROJECT_NAME}
install(DIRECTORY include/
  DESTINATION include/${PROJECT_NAME}
  FILES_MATCHING PATTERN "*.h")

# Copy the created .ta into install/lib/${PROJECT_NAME}
install(FILES
  src/${TA_BINARY_NAME}.ta
  DESTINATION lib/${PROJECT_NAME}
)

ament_package()
